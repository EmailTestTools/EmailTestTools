        name =  elements crlf
        b           =  binary
        d           =  decimal
        x           =  hexadecimal
        CR          =  %d13
        CR          =  %x0D
        CRLF        =  %d13.10
        command     =  "command string"
        rulename = "abc"
        rulename = "aBc"
        rulename    =  %d97 %d98 %d99
        rulename    =  %d97.98.99
        foo         =  %x61           ; a
        bar         =  %x62           ; b
        mumble      =  foo bar foo
        ruleset     =  alt1 / alt2
        ruleset     =  alt1 / alt2 / alt3 / alt4 / alt5
        DIGIT       =  %x30-39
        DIGIT       =  "0" / "1" / "2" / "3" / "4" / "5" / "6" /
        char-line = %x0D.0A %x20-7E %x0D.0A
        rulelist       =  1*( rule / (*c-wsp c-nl) )
        rule           =  rulename defined-as elements c-nl
                               ; continues if next line starts
                               ;  with white space
        rulename       =  ALPHA *(ALPHA / DIGIT / "-")
        defined-as     =  *c-wsp ("=" / "=/") *c-wsp
                               ; basic rules definition and
                               ;  incremental alternatives
        elements       =  alternation *c-wsp
        c-wsp          =  WSP / (c-nl WSP)
        c-nl           =  comment / CRLF
                               ; comment or newline
        comment        =  ";" *(WSP / VCHAR) CRLF
        alternation    =  concatenation
                          *(*c-wsp "/" *c-wsp concatenation)
        concatenation  =  repetition *(1*c-wsp repetition)
        repetition     =  [repeat] element
        repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)
        element        =  rulename / group / option /
                          char-val / num-val / prose-val
        group          =  "(" *c-wsp alternation *c-wsp ")"
        option         =  "[" *c-wsp alternation *c-wsp "]"
        char-val       =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
                               ; quoted string of SP and VCHAR
                                  without DQUOTE
        num-val        =  "%" (bin-val / dec-val / hex-val)
        bin-val        =  "b" 1*BIT
                          [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                               ; series of concatenated bit values
                               ; or single ONEOF range
        dec-val        =  "d" 1*DIGIT
                          [ 1*("." 1*DIGIT) / ("-" 1*DIGIT) ]
        hex-val        =  "x" 1*HEXDIG
                          [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG) ]
        prose-val      =  "<" *(%x20-3D / %x3F-7E) ">"
                               ; bracketed string of SP and VCHAR
                                  without angles
                               ; prose description, to be used as
                                  last resort
        ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z
        BIT            =  "0" / "1"
        CHAR           =  %x01-7F
                               ; any 7-bit US-ASCII character,
                                  excluding NUL
        CR             =  %x0D
                               ; carriage return
        CRLF           =  CR LF
                               ; Internet standard newline
        CTL            =  %x00-1F / %x7F
                               ; controls
        DIGIT          =  %x30-39
                               ; 0-9
        DQUOTE         =  %x22
                               ; " (Double Quote)
        HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
        HTAB           =  %x09
                               ; horizontal tab
        LF             =  %x0A
                               ; linefeed
        LWSP           =  *(WSP / CRLF WSP)
                               ; linear white space (past newline)
        OCTET          =  %x00-FF
                               ; 8 bits of data
        SP             =  %x20
        VCHAR          =  %x21-7E
                               ; visible (printing) characters
        WSP            =  SP / HTAB
                               ; white space